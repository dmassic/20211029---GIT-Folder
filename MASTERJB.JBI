/JOB
//NAME MASTERJB
///FOLDERNAME MASTER
//POS
///NPOS 0,0,0,0,0,0
//INST
///DATE 2021/10/29 11:49
///COMM YMCL AWIV 6000SL MASTER V1.0
///ATTR SC,RO
NOP
'*******************************
'MASTER JOB FOR AWIV-6000SL
'CELL WITH AN MRM2-755-M3X POS.
'AND AN MA2010 DX200 ROBOT.
'*******************************
'Variable B030=1, A at robot.
'Variable B031=1, B at robot.
'Variable B032=1, Weld inhibit.
'Initialize system. Go to ready.
CALL JOB:REDY2010
DOUT OT#(1817) OFF
WAIT IN#(1817)=OFF
DOUT OT#(1818) OFF
WAIT IN#(1818)=OFF
DOUT OT#(1) OFF
*START
'Start of main control loop.
JUMP *FAULT-10 IF IN#(4089)=OFF
JUMP *START-A IF IN#(6)=ON
JUMP *START-B IF IN#(5)=ON
PULSE OT#(1) T=0.50
TIMER T=1.00
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'WARNING, THE POSITIONER IS NOT
'IN A SAFE POSITION TO START.
'TEACH THE POSITIONER TO THE
'FINAL END POSITION AND RESTART.
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
SET B032 1
JUMP *START
*START-A
'Fix-A at load, fix-B at robot.
JUMP *FAULT-1 IF IN#(6)=OFF
PSTART JOB:JOG_S2 SUB4 IF IN#(1817)=OFF
CALL JOB:HOME_B
JUMP *START IF IN#(3)=OFF
JUMP *SWEEP-A IF IN#(1)=ON
JUMP *START
*START-B
'Fix-B at load, fix-A at robot.
JUMP *FAULT-2 IF IN#(5)=OFF
PSTART JOB:JOG_S3 SUB4 IF IN#(1818)=OFF
CALL JOB:HOME_A
JUMP *START IF IN#(3)=OFF
JUMP *SWEEP-B IF IN#(1)=ON
JUMP *START
*SWEEP-A
'Sweep fixture-A to robot.
JUMP *FAULT-1 IF IN#(6)=OFF
DOUT OT#(1817) OFF
PWAIT SUB4
CALL JOB:SIDE-A-CLAMP
JUMP *FAULT12 IF B099=0
PSTART JOB:SWEEP_A SUB3
PWAIT SUB3
PWAIT SUB1
SET B031 0
SET B030 1
PSTART JOB:JOG_S3 SUB4 IF IN#(1818)=OFF
JUMP *RESET IF B032>0
JUMP *WELD-A IF IN#(2)=ON
WAIT IN#(1)=OFF T=3.00
JUMP *FAULT-9 IF IN#(1)=ON
TIMER T=0.20
JUMP *START
*SWEEP-B
'Sweep fixture-B to robot.
JUMP *FAULT-2 IF IN#(5)=OFF
DOUT OT#(1818) OFF
PWAIT SUB4
CALL JOB:SIDE-B-CLAMP
JUMP *FAULT12 IF B099=0
PSTART JOB:SWEEP_B SUB3
PWAIT SUB3
PWAIT SUB1
SET B030 0
SET B031 1
PSTART JOB:JOG_S2 SUB4 IF IN#(1817)=OFF
JUMP *RESET IF B032>0
JUMP *WELD-B IF IN#(2)=ON
WAIT IN#(1)=OFF T=3.00
JUMP *FAULT-9 IF IN#(1)=ON
TIMER T=0.20
JUMP *START
*WELD-A
'Ready to weld fixture-A.
CALL JOB:SIDE_A
JUMP *CLEAN IF I010>=I011
TIMER T=0.20
JUMP *START
*WELD-B
'Ready to weld fixture-B.
CALL JOB:SIDE_B
JUMP *CLEAN IF I010>=I011
TIMER T=0.20
JUMP *START
*CLEAN
'Clean request for the operator
CALL JOB:CLEANREQ
PULSE OT#(1) T=0.50
TIMER T=0.50
PAUSE
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'     OPERATOR MUST CLEAN
'        THE NOZZLE
'    Go inside the cell
'   and clean the nozzle
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
CALL JOB:REDY2010
JUMP *START
*RESET
'Reset B032 weld inhibit variable
'to 0 to allow welding of parts.
SET B032 0
WAIT IN#(1)=OFF T=3.00
JUMP *FAULT-9 IF IN#(1)=ON
TIMER T=0.20
JUMP *START
*FAULT-1
PULSE OT#(1) T=0.50
TIMER T=0.50
PAUSE
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'WARNING, THE POSITIONER IS NOT
'IN THE PROPER POSITION TO RUN
'FROM THIS LABEL.SIDE B AT ROBOT
'      (CHECK INPUT #6)
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
JUMP *START
*FAULT-2
PULSE OT#(1) T=0.50
TIMER T=0.50
PAUSE
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'WARNING, THE POSITIONER IS NOT
'IN THE PROPER POSITION TO RUN
'FROM THIS LABEL.SIDE A AT ROBOT
'      (CHECK INPUT #5)
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
JUMP *START
*FAULT-9
PULSE OT#(1) T=0.50
TIMER T=0.50
PAUSE
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'WARNING, THE SWEEP START INPUT
'DID NOT RELEASE! PLEASE CHECK
'FOR DEFECTIVE ANTI-TIE DOWN
'CIRCUIT. (CHECK INPUT #1)
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
JUMP *START
*FAULT-10
PULSE OT#(1) T=0.50
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'DANGER,ROBOT 1 IS NOT AT THE
'SAFE POSITION TO SWEEP THE
'POSITIONER. PLEASE TEACH THE
'ROBOT TO THE READY POSITION,
'THEN RESTART THIS JOB AT THE
'NOP COMMAND.(CHECK INPUT #4089)
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
JUMP *START
*FAULT12
PAUSE
'CLAMPING
'NOT COMPLETE
JUMP *START
END
