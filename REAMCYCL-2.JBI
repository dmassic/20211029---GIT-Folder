/JOB
//NAME REAMCYCL-2
///FOLDERNAME CLEAN
//POS
///NPOS 0,0,0,0,0,0
//INST
///DATE 2021/10/29 11:50
///COMM YMCL FRONIUS REAMER CYCLE JOB
///ATTR SC,RO
NOP
'*******************************
'CALL THIS JOB TO CYCLE THE
'NOZZLE REAMER. FRONIUS V EASY.
'*******************************
*RETRY
DOUT OT#(9) ON
WAIT IN#(12)=OFF T=5.00
JUMP *FAULT-1 IF IN#(12)=ON
WAIT IN#(13)=ON T=5.00
JUMP *FAULT-1 IF IN#(13)=OFF
DOUT OT#(10) ON
WAIT IN#(15)=OFF T=5.00
JUMP *FAULT-1 IF IN#(15)=ON
WAIT IN#(16)=ON T=10.00
JUMP *FAULT-2 IF IN#(16)=OFF
'Nozzle ream time.
TIMER T=0.50
DOUT OT#(10) OFF
WAIT IN#(16)=OFF T=5.00
JUMP *FAULT-2 IF IN#(16)=ON
WAIT IN#(15)=ON T=5.00
JUMP *FAULT-2 IF IN#(15)=OFF
DOUT OT#(9) OFF
WAIT IN#(13)=OFF T=5.00
JUMP *FAULT-2 IF IN#(13)=ON
WAIT IN#(12)=ON T=5.00
JUMP *FAULT-2 IF IN#(12)=OFF
JUMP *SPRY_SKP IF B001=1
'SPRAY
TIMER T=0.50
*SPRY_SKP
RET
*FAULT-1
DOUT OT#(9) OFF
DOUT OT#(10) OFF
PAUSE
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'WARNING, NOZZLE REAMER DID NOT
'CYCLE. CHECK FOR LOW AIR
'PRESSURE OR JAMMED MECHANISM.
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
JUMP *RETRY
*FAULT-2
DOUT OT#(9) OFF
DOUT OT#(10) OFF
PAUSE
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'WARNING, NOZZLE REMER DID NOT
'COMPLETE CYCLE. CHECK FOR
'JAMMED MECHANISM.
'!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
JUMP *RETRY
END
